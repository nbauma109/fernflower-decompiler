// Copyright 2000-2021 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license that can be found in the LICENSE file.
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.frgaal:compiler-gradle-plugin:15.0.0"
    }
}

apply plugin: 'java'
apply plugin: 'org.frgaal.compiler'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

targetCompatibility = '1.8'
sourceCompatibility = '15'

compileJava {
    options.compilerArgs << '-Xlint:deprecation' << '--enable-preview'
}
compileTestJava {
    options.compilerArgs << '-Xlint:deprecation' << '--enable-preview'
}

sourceSets {
  main.java.srcDirs 'src'
  test.java.srcDirs 'test'
}

repositories { mavenCentral() }
dependencies {
  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:3.12.2'
}

jar {
  archiveName 'fernflower.jar'
  manifest {
    attributes 'Main-Class': 'org.jetbrains.java.decompiler.main.decompiler.ConsoleDecompiler'
  }
}
